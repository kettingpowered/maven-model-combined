plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

group = 'org.kettingpowered'
version = System.getenv("VERSION") ?: 'dev-ver'

repositories {
    mavenCentral()
}

configurations {
    pack { transitive = false }
}

dependencies {
    pack libs.bundles.maven.model.combined
}

shadowJar {
    configurations = [project.configurations.pack]

    archiveBaseName.set('maven-model-combined')
    archiveClassifier.set('')
    archiveExtension.set('jar')

    manifest {
        attributes 'Specification-Title': 'Kettingpowered',
                'Specification-Vendor': 'Kettingpowered',
                'Implementation-Title': 'maven-model-combined',
                'Implementation-Version': version,
                'Implementation-Vendor': 'Kettingpowered'
    }

    mergeServiceFiles()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        myPublication(MavenPublication) {
            from components.java
            artifact(sourcesJar) {
                // These values will be used instead of the values from the task. The task values will not be updated.
                classifier "sources"
                extension "jar"
            }
            artifact(javadocJar) {
                //These values will be used instead of the values from the task. The task values will not be updated.
                classifier "javadoc"
                extension "jar"
            }
            pom {
                name = "maven-model-combined"
                description = "An API supported across all Ketting Versions"
                url = "https://github.com/kettingpowered/maven-model-combined"
                licenses {
                    license {
                        name = "MIT License"
                        url = "http://www.opensource.org/licenses/mit-license.php"
                    }
                }
                developers {
                    developer {
                        id = "justred23"
                        name = "JustRed23"
                        //email = "jon@doe"
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/kettingpowered/maven-model-combined.git"
                    //developerConnection = "scm:svn:https://subversion.example.com/svn/project/trunk/"
                    url = "https://github.com/kettingpowered/maven-model-combined"
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/kettingpowered/maven-model-combined")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
        maven {
            name = 'kettingRepo'
            credentials {
                username = System.getenv("KETTINGUSERNAME")
                password = System.getenv("KETTINGPASSWORD")
            }
            url = "https://nexus.c0d3m4513r.com/repository/Ketting/"
        }
    }
}